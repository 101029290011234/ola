#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(configure.in)
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(lla, 0.2.3)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL
AC_PROG_RANLIB

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h netinet/in.h stdint.h stdlib.h string.h sys/ioctl.h sys/socket.h sys/time.h syslog.h termios.h unistd.h])
AC_CHECK_HEADER(pthread.h, have_threads=posix)


AM_CONDITIONAL(HAVE_PTHREAD, test "${have_threads}" = "posix")
if test "${have_threads}" = "posix"; then
  AC_DEFINE(HAVE_PTHREAD, 1, [define if pthread is installed])
fi


# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_HEADER_TIME
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT8_T

# Checks for library functions.
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_FORK
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_SELECT_ARGTYPES
AC_FUNC_STAT
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([bzero gettimeofday memmove memset mkdir strdup strrchr inet_ntoa select socket strerror])

# Check if sockaddr{} has sa_len member.
AC_CHECK_MEMBER([struct sockaddr.sa_len],
 AC_DEFINE(HAVE_SOCKADDR_SA_LEN, 1, define if socket address structures have length fields),,[
#include <sys/types.h>
#include <sys/socket.h>])

# check for plugin dependencies. We also build a list of plugins that we're
# going to compile here so the test binary knows what to link against
PLUGINS="dummy opendmx stageprofi usbpro"

PLUGIN_LIBRARY(artnet, artnet_new, ARTNET)
PLUGIN_LIBRARY(dmx4linux, DMXdev, DMX4LINUX)
PLUGIN_LIBRARY(eartnet, eartnet_new, EARTNET)
PLUGIN_LIBRARY(espnet, espnet_new, ESPNET)
PLUGIN_LIBRARY(shownet, shownet_new, SHOWNET)
PLUGIN_LIBRARY(sandnet, sandnet_new, SANDNET)
PLUGIN_LIBRARY(pathport, pathport_new, PATHPORT)

# create a list of plugin libs that will be used for testing
PLUGIN_LIBS=''
for p in $PLUGINS
do PLUGIN_LIBS="$PLUGIN_LIBS \$(top_builddir)/plugins/$p/liblla$p.la"
done

AC_SUBST(PLUGIN_LIBS)

# plugin dir
plugindir=$libdir/llad
AC_SUBST(plugindir)

LLAD_DEFINES="$CFLAGS -DPLUGIN_DIR=\\\"$plugindir\\\" "
AC_SUBST(LLAD_DEFINES)

AC_ARG_ENABLE(debug, [AS_HELP_STRING(--enable-debug,
                     [Enable debugging])],
AC_DEFINE(DEBUG, 1, [Enable debugging]) )


# python wrappers
AC_ARG_ENABLE(python-libs,
 [  --enable-python-libs Build the python wrappers],
 [ build_python_libs="yes"]
)

AM_CONDITIONAL(BUILD_PYTHON_LIBS, test "${build_python_libs}" = "yes")

if test "${build_python_libs}" = "yes"; then
 AM_PATH_PYTHON(2.3)
 AC_PROG_SWIG(1.3.21)
 SWIG_ENABLE_CXX
 SWIG_PYTHON
fi


AC_OUTPUT_COMMANDS([
  if test -n "$CONFIG_FILES" && test -n "$CONFIG_HEADERS"; then
    # If both these vars are non-empty, then config.status wasn't run by
    # automake rules (which always set one or the other to empty).
    CONFIG_OTHER=${CONFIG_OTHER-lla/common.h}
  fi
  echo $CONFIG_OTHER
  case "$CONFIG_OTHER" in
  *lla/common.h*)
    outfile=lla/common.h
    stampfile=lla/stamp-common
    tmpfile=${outfile}T
    dirname="sed s,^.*/,,g"

    echo creating $outfile
    cat > $tmpfile << _EOF_
/*  -*- Mode: C -*-
 * --------------------------------------------------------------------
 * DO NOT EDIT THIS FILE!  It has been automatically generated
 * from:    configure.in and `echo $outfile|$dirname`.in
 * on host: `(hostname || uname -n) 2>/dev/null | sed 1q`
 * --------------------------------------------------------------------
 */

#ifndef LLA_COMMON_H
#define LLA_COMMON_H 1

_EOF_

    if test x$have_threads = xposix; then
      echo '#define LLA_HAVE_PTHREAD 1' >> $tmpfile
    fi

    # The ugly but portable cpp stuff comes from here
    infile=$srcdir/lla/`echo $outfile | sed 's,.*/,,g;s,\..*$,,g'`-h.in
    sed '/^##.*$/d' $infile >> $tmpfile

    cat >> $tmpfile << '_EOF_'

#endif /* LLA_COMMON_H */
_EOF_

    if cmp -s $tmpfile $outfile; then
      echo $outfile is unchanged
      rm -f $tmpfile
    else
      mv $tmpfile $outfile
      touch $stampfile
    fi
  esac;
],[
  srcdir=$srcdir
  have_threads=$have_threads
])

# output
AC_OUTPUT( Makefile \
    include/Makefile\
    include/lla/Makefile\
    include/llad/Makefile\
    lla/Makefile \
    python/Makefile \
    llad/Makefile \
    plugins/Makefile \
    plugins/artnet/Makefile \
    plugins/artnet/conf_msgs/Makefile \
    plugins/eartnet/Makefile \
    plugins/dummy/Makefile \
    plugins/dmx4linux/Makefile \
    plugins/espnet/Makefile \
    plugins/pathport/Makefile \
    plugins/shownet/Makefile \
    plugins/sandnet/Makefile \
    plugins/usbpro/Makefile \
    plugins/stageprofi/Makefile \
    plugins/usbpro/conf_msgs/Makefile \
    plugins/opendmx/Makefile \
    liblla.pc \
    plugins/usbpro/conf_msgs/libllausbproconf.pc \
    plugins/artnet/conf_msgs/libllaartnetconf.pc
    )
